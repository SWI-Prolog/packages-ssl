dnl Process this file with autoconf to produce a configure script.

AC_INIT(install-sh)
AC_PREREQ([2.50])
AC_CONFIG_HEADER(config.h)

AC_SUBST(TARGETS)
AC_SUBST(PLTARGETS)

m4_include([../ac_swi_c.m4])

case "$PLARCH" in
    *-win32|*-win64)
        AC_CHECK_LIB(ws2_32, main)
        AC_CHECK_LIB(gdi32, main)
	AC_CHECK_LIB(crypt32, main)
        ;;
    *darwin*)
	LIBS="$LIBS -framework CoreFoundation -framework Security"
	;;
esac

AC_CHECK_HEADERS(unistd.h fcntl.h Security/Security.h)
AC_MSG_CHECKING(kSecClass)
AC_TRY_COMPILE(
[ #include <sys/types.h>
  #include <Security/Security.h>
],
[ const void *key = kSecClass;
], AC_DEFINE(HAVE_KSECCLASS, 1,
	     [Define if Security/Security.h supplies kSecClass])
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no))

AC_CHECK_LIB(crypto, main)
AC_CHECK_LIB(ssl, SSL_library_init)
AC_CHECK_LIB(ssl, OPENSSL_init_ssl)
AC_CHECK_TYPES(socklen_t, [], [],
[
#include <sys/types.h>
#include <sys/socket.h>
])

if ( test "$ac_cv_lib_ssl_SSL_library_init" = yes ||
     test "$ac_cv_lib_ssl_OPENSSL_init_ssl" = yes ) &&
   test	"$ac_cv_lib_crypto_main" = yes; then
  TARGETS="ssl4pl.$SO"
  PLTARGETS="ssl.pl saml.pl xmldsig.pl xmlenc.pl"
else
  echo "ERROR:   Cannot find ssl library"
  echo "WARNING: SSL interface will not be built"
  TARGETS=""
  PLTARGETS=""
fi

AC_MSG_CHECKING(h_errno)
AC_TRY_COMPILE(
[
#include <sys/types.h>
#include <sys/socket.h>
#include <netdb.h>
#include <netinet/in.h>
],
[ int x = h_errno;
], AC_DEFINE(HAVE_H_ERRNO, 1,
	     [Define of h_errno is provided])
   AC_MSG_RESULT(yes),
   AC_MSG_RESULT(no))

AC_CHECK_FUNCS(timegm ERR_remove_state ERR_remove_thread_state)
AC_CHECK_FUNCS(X509_check_host)
AC_CHECK_FUNCS(CRYPTO_THREADID_get_callback CRYPTO_THREADID_set_callback)
AC_CHECK_FUNCS(EVP_MD_CTX_free OPENSSL_zalloc)

AC_CHECK_TYPES(CRYPTO_THREADID, [], [],
[
#include <openssl/ssl.h>
])
AC_CHECK_MEMBERS(X509_VERIFY_PARAM.id,
[ AC_DEFINE(HAVE_X509_VERIFY_PARAM_ID, 1,
	    [Define of X509_VERIFY_PARAM_ID is provided])
], [],
[
#include <openssl/ssl.h>
])

AC_MSG_CHECKING(system certificate file)
SYSTEM_CACERT_FILENAME=/etc/ssl/certs/ca-certificates.crt
cert_candidates="/etc/ssl/certs/ca-certificates.crt"
cert_candidates+=" /etc/pki/tls/certs/ca-bundle.crt"
cert_candidates+=" /etc/ssl/ca-bundle.pem"

for f in $cert_candidates; do
  if test -f $f; then
    SYSTEM_CACERT_FILENAME=$f
    break
  fi
done
AC_DEFINE_UNQUOTED(SYSTEM_CACERT_FILENAME, "$SYSTEM_CACERT_FILENAME",
		   [Location of the system certificate file])
AC_MSG_RESULT($SYSTEM_CACERT_FILENAME)

AC_OUTPUT(Makefile)




